// These files contain definitions of classes. Not all of them will be defined during any actual
// game; that depends on how the game was configured. However, if a class is defined then it forces
// every other class its definition refers to to be defined as well (with the possible exception of
// effect *triggers*, since we can reason tht those triggers would simply never happen anyway.

"A catch-all for arbitrary global effects that don't seem to belong anywhere else"
CLASS TerraformingMars : System, AutoLoad {
  HAS =1 This

  SetupPhase: Generation
  PlayTag<Class<BuildingTag>>:: Accept<Class<Steel>>
  PlayTag<Class<SpaceTag>>:: Accept<Class<Titanium>>
  Pay<Class<Steel>>:: -2 Owed
  Pay<Class<Titanium>>:: -3 Owed                       // should be innocuous for TradeSA...
}

"A single step along a global parameter track; creating one gives the active player (if any) a TR"
ABSTRACT CLASS GlobalParameter : Atomized {
  DEFAULT +GlobalParameter.                            // by default gains/removes are "if possible"
  DEFAULT -GlobalParameter.

  This: TerraformRating                                // transformed to `This BY Owner: TR<Owner>`

  "Represents 2 degrees C of temperature increase"
  CLASS TemperatureStep[TEMP] {
    HAS MAX 19 This
    This IF =3 This: PROD[Heat]
    This IF =5 This: PROD[Heat]
    This IF =15 This: OceanTile
  }

  "Represents a 1% increase in oxygen level"
  CLASS OxygenStep[O2] {
    HAS MAX 14 This
    This IF =8 This: TemperatureStep
  }

  "Represents a 2% increase in Venus... terraformedness"
  CLASS VenusStep[VEN] {
    HAS MAX 15 This
    This IF =4 This: ProjectCard
    This IF =8 This: TerraformRating
  }
  // OceanTile is in maps-tiles.pets
}

"Tells what generation we're in; mostly matters in solo games"
CLASS Generation

"Supertype for anything that automatically disappears between generations"
ABSTRACT CLASS Generational { Generation:: -This. }

"The current game phase; there is always exactly one of these in existence"
ABSTRACT CLASS Phase : AutoLoad {
  HAS =1 Phase                                         // weird that this works, but it does

  CLASS SetupPhase
  CLASS CorporationPhase {
    NewTurn: PlayCard<Class<CorporationCard>>, 10 BuyCard?
  }
  CLASS PreludePhase {
    NewTurn: PlayCard<Class<PreludeCard>> OR (-PreludeCard THEN 15) // ELSE (#22)
  }
  CLASS ActionPhase {
    NewTurn: UseAction<StandardAction> OR Pass
  }
  CLASS ProductionPhase {
    This: Production2Phase FROM This
  }
  "An unfortunate implementation detail seemingly forced by Supercapacitors :-("
  CLASS Production2Phase
  CLASS ResearchPhase { This:: Generation }
  CLASS EndPhase : AutoLoad { This:: End }
}

"This Signal causes components to pay out victory points, after which the game terminates"
CLASS End : Signal
