//
// Colonies expansion!
//
// Colony tile data read from colonies.json5 is converted to classes in this form:
//
//   CLASS $name : ColonyTile {
//     Colony<This>: $placementBonus
//     GiveColonyBonus<This>: $colonyBonus
//     Trade<This> IF =0 ColonyProduction<This>: ${tradeIncome[0]} // etc.
//     Trade<This>: ResetProduction<This>
//   }

"A colony tile, such as Pluto"
ABSTRACT CLASS ColonyTile {
  HAS MAX 1 This,  MAX 3 Colony<This>, MAX 1 Trade<This>

  // WHITE CUBE MAINTENANCE

  // Start at 1, not 0
  This:: ColonyProduction<This>

  // When colony built, move it right if necessary
  Colony<This>: -ColonyProduction<This>. / Colony<This> THEN ColonyProduction<This> / Colony<This>

  // After trade, move it all the way left
  ResetProduction<This>:: -6 ColonyProduction<This>. THEN ColonyProduction<This> / Colony<This, Anyone>

  // At end of gen, move it right one (technically during the "colonies solar phase" but shrug)
  ProductionPhase: ColonyProduction<This>.
}

"One of the up-to-three colonies players can own on a ColonyTile"
CLASS Colony<ColonyTile> : Owned {
  DEFAULT +Colony<ColonyTile(HAS MAX 0 Colony)>
  Trade<Anyone, ColonyTile>: GiveColonyBonus<ColonyTile>
}

"A Trade operation; it persists so we can tell the ColonyTile and TradeFleet are occupied"
// A trade fleet sitting on a colony tile is represented by one of these, which go away at gen end
CLASS Trade<ColonyTile, TradeFleet> : Owned, Generational

"A notch on the production track of a ColonyTile; note a tile has 0 to 6 of these (not 1 to 7)"
CLASS ColonyProduction<ColonyTile> { HAS MAX 6 This }

"An implementation detail used to trigger colony bonuses to be paid out"
CLASS GiveColonyBonus<ColonyTile> : Owned, Signal

"An implementation detail used to trigger a colony tile's 'white cube' to move left"
CLASS ResetProduction<ColonyTile> : Signal

"One of the 8 trade fleets players can own; sadly each is distinct even though you don't care about the difference"
ABSTRACT CLASS TradeFleet : Owned {
  HAS MAX 1 Trade<This>

  CLASS TradeFleetA { HAS MAX 1 TradeFleetA }
  CLASS TradeFleetB { HAS MAX 1 TradeFleetB }
  CLASS TradeFleetC { HAS MAX 1 TradeFleetC }
  CLASS TradeFleetD { HAS MAX 1 TradeFleetD }
  CLASS TradeFleetE { HAS MAX 1 TradeFleetE }
  CLASS TradeFleetF { HAS MAX 1 TradeFleetF }
  CLASS TradeFleetG { HAS MAX 1 TradeFleetG }
  CLASS TradeFleetH { HAS MAX 1 TradeFleetH }
}

"To add a colony tile to the game (like Aridor does), gain this"
CLASS AddColonyTile<Class<ColonyTile>> : Custom, AutoLoad

"If a ColonyTile gives card resources, one of these is created first instead"
CLASS DelayedColonyTile<Class<ColonyTile>, Class<CardResource>> {
  This IF ResourceCard<Class<CardResource>>:: ColonyTile FROM This // thanks Aridor
  ResourceCard<Class<CardResource>>: ColonyTile FROM This
}
